fs hlbGit() {
	git "https://github.com/openllb/hlb.git" "1684b5c90e2ec052dfe2fab6a7026b30a056df85"
}

fs hlbLocalSource() {
    local "."
}

fs goBuild(fs src, string package) {
	image "golang:1.12-alpine"
	run "apk" "add" "-U" "git" "gcc" "libc-dev"
	env "GO111MODULE" "on"
	dir "/go/src/hlb"
	run string {
		format "/usr/local/go/bin/go build -o /out/binary -ldflags '-linkmode external -extldflags -static' -a %s" package
	} with option {
		mount src "/go/src/hlb"
		mount fs { scratch; } "/out" as goBinary
        	mount fs { scratch; } "/root/.cache/go-build" with option {
			cache "hlb/go-build" "private"
        	}
        	mount fs { scratch; } "/go/pkg/mod" with option {
			cache "hlb/go-mod" "private"
		}
	}
}

fs hlbFrontend() {
	scratch
	copy fs { goBinary hlbGit "./cmd/frontend"; } "/binary" "/run"
	copy hlbGit "/source.hlb" "/"
	copy hlbGit "/signature.hlb" "/"
}

fs hlbCI() {
	scratch
	copy fs { goBinary hlbGit "./cmd/hlb"; } "/binary" "/hlb"
}

fs hlbLocal() {
	scratch
	copy fs { goBinary hlbLocalSource "./cmd/hlb"; } "/binary" "/hlb"
}
